# Maintainer: yifwon <wyf9661 at gmail dot com>
pkgbase=sylixos-cross-compiler-toolchain
pkgname=('sylixos-cross-compiler-toolchain' 'sylixos-cross-compiler-lcsproxy' 'sylixos-cross-compiler-senseshield' 'sylixos-cmake-plugins')
pkgver=1.0.0
_lcsproxy_ver=1.0.8
_senseshield_ver=2.7.0.66418
pkgrel=1
pkgdesc="cross compile toolchain to build objects running on sylixos"
arch=('x86_64')
url="https://armory.acoinfo.com"
_filepath="@realevo/realevo-linux-tools@${pkgver}"
license=('custom')
makedepends=('armory-cli')
depends=('systemd')
options=(!strip)
DLAGENTS=("https::/usr/bin/armory get ${_filepath} %o")
source=("${_filepath}/realevo-linux-tools-v$pkgver.tar.gz::$url/realevo-linux-tools-v$pkgver.tar.gz")
sha1sums=('2373368cce23e869eca6be39dcc2d3b38c968cad')

prepare() {
    cd ../${_filepath}
    tar --no-same-owner --no-same-permissions -xavf realevo-linux-tools-v$pkgver.tar.gz -C ${srcdir}
}

package_sylixos-cross-compiler-toolchain() {
    _install_dir="opt/sylixos"

    install -dm755 "${pkgdir}/${_install_dir}"
    cp -r "${srcdir}/realevo-linux-tools/compiler" "${pkgdir}/${_install_dir}"

    #symlinking
    install -dm755 "${pkgdir}/usr/bin"
    for platform in ${pkgdir}/${_install_dir}/compiler/*-toolchain; do
        for component in $platform/bin/*; do
            ln -sf ${component##$pkgdir} ${pkgdir}/usr/bin/
        done
    done
}

package_sylixos-cross-compiler-lcsproxy() {
    backup=("etc/lcsproxy/lcsproxy.conf")

    ar x ${srcdir}/realevo-linux-tools/pkg/lcsproxy-${_lcsproxy_ver}-linux-amd64.deb
    tar --no-same-owner --no-same-permissions -xavf data.tar.gz -C ${pkgdir}
    mv ${pkgdir}/bin ${pkgdir}/usr
}

package_sylixos-cross-compiler-senseshield() {
    ar x ${srcdir}/realevo-linux-tools/pkg/senseshield-lcc-${_senseshield_ver}-amd64.deb
    tar --no-same-owner --no-same-permissions -xavf data.tar.xz -C ${pkgdir}
}

package_sylixos-cmake-plugins() {
    replace=("cmake-sylixos-plugins")
    conflicts=("cmake-sylixos-plugins")
    provides=("cmake-sylixos-plugins")

    for plugin in SylixOS.cmake SylixOS-GNU.cmake SylixOS-GNU-C.cmake SylixOS-GNU-CXX.cmake SylixOS-GNU-Fortran.cmake;do
        install -Dm644 "${srcdir}/realevo-linux-tools/config/cmake/platform/${plugin}" "${pkgdir}/usr/share/cmake/Modules/Platform/${plugin}"
    done
}
