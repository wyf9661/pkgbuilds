# Maintainer: yifwon <wyf9661 at gmail dot com>
pkgbase=realevo-linux-tools
pkgname=(${pkgbase}-{compiler,lcsproxy,cmake-plugins,base})
pkgver=1.1.0
_lcsproxy_ver=1.0.8
_senseshield_ver=2.7.0.66418
pkgrel=1
pkgdesc="cross compile toolchain to build objects running on sylixos"
arch=('x86_64')
url="https://armory.acoinfo.com"
license=('custom')
makedepends=('amr')
depends=('systemd')
options=(!strip)
DLAGENTS=("http::/usr/bin/amr %u -o %o")
source=($pkgbase-$pkgver-linux.zip::"http://10.7.1.31/armory/v1/packages/@realevo/$pkgbase/v/$pkgver/p/Linux")
sha1sums=('cd0c2623f96898b51ad9378cb28261b6eb1785d0')

prepare() {
    ar x $pkgbase-v$pkgver.deb
    bsdtar -xf data.tar.zst
}

_install_dir="opt/${pkgbase}"

package_realevo-linux-tools-compiler() {
    optdepends=('ncurses5-compat-libs: libncurses.so.5 provided for gdb')
    install -dm755 "${pkgdir}/${_install_dir}"
    cp -r "${srcdir}/opt/acoinfo/${pkgbase}/compiler" "${pkgdir}/${_install_dir}"

    #symlinking
    install -dm755 "${pkgdir}/usr/bin"
    for platform in ${pkgdir}/${_install_dir}/compiler/*-toolchain; do
        for component in $platform/bin/*; do
            ln -sf ${component##$pkgdir} ${pkgdir}/usr/bin/
        done
    done
}

package_realevo-linux-tools-base() {
    install -dm755 "${pkgdir}/${_install_dir}"
    cp -r "${srcdir}/opt/acoinfo/${pkgbase}/pkg/base" "${pkgdir}/${_install_dir}"
}

package_realevo-linux-tools-lcsproxy() {
    backup=("etc/lcsproxy/lcsproxy.conf")

    ar x ${srcdir}/opt/acoinfo/${pkgbase}/pkg/lcsproxy-${_lcsproxy_ver}-linux-amd64.deb
    tar --no-same-owner --no-same-permissions -xavf data.tar.gz -C ${pkgdir}
    mv ${pkgdir}/bin ${pkgdir}/usr
}

package_realevo-linux-tools-cmake-plugins() {
    for plugin in SylixOS.cmake SylixOS-GNU.cmake SylixOS-GNU-C.cmake SylixOS-GNU-CXX.cmake SylixOS-GNU-Fortran.cmake;do
        install -Dm644 "${srcdir}/opt/acoinfo/${pkgbase}/config/cmake/platform/${plugin}" "${pkgdir}/usr/share/cmake/Modules/Platform/${plugin}"
    done
}
