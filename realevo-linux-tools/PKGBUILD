# Maintainer: yifwon <wyf9661 at gmail dot com>
pkgbase=realevo-linux-tools
pkgname=(${pkgbase}-{compiler,lcsproxy,cmake-plugins,base})
pkgver=1.1.0
_lcsproxy_ver=1.1.0
_lcsproxy_name=realevo_license_sdk
_prefix_path="http://10.7.1.31/armory/v1/packages/blob/@realevo"
pkgrel=2
pkgdesc="cross compile toolchain to build objects running on sylixos"
arch=('x86_64')
url="https://armory.acoinfo.com"
license=('custom')
makedepends=('amr')
depends=('systemd')
options=(!strip)
DLAGENTS=("http::/usr/bin/amr %u -o %o")
source=($pkgbase-v$pkgver.deb::"${_prefix_path}/${pkgbase}/v/${pkgver}/$pkgbase-v$pkgver.deb"
        compile-linux.tar.gz::"http://10.13.42.54/compile-linux.tar.gz"
        ${_lcsproxy_name}.zip::"${_prefix_path}/${_lcsproxy_name}/v/${_lcsproxy_ver}/linux/${_lcsproxy_name}.zip")
sha1sums=('2b93835a35391146e412fd5ca0044e6a73e4b554'
          '7c676ad3efb8e5fb04845c00be68c1f992a9a4a3'
          '2d5d7db7d1ffa3569d7e918114d9b4a30235a165')

prepare() {
    bsdtar -xf data.tar.zst
}

_install_dir="opt/${pkgbase}"

package_realevo-linux-tools-compiler() {
    optdepends=('ncurses5-compat-libs: libncurses.so.5 provided for gdb')
    install -dm755 "${pkgdir}/${_install_dir}"
    cp -r "${srcdir}/compiler" "${pkgdir}/${_install_dir}"

    #symlinking
    install -dm755 "${pkgdir}/usr/bin"
    for platform in ${pkgdir}/${_install_dir}/compiler/*-toolchain; do
        for component in $platform/bin/*; do
            ln -sf ${component##$pkgdir} ${pkgdir}/usr/bin/
        done
    done
}

package_realevo-linux-tools-base() {
    install -dm755 "${pkgdir}/${_install_dir}"
    cp -r "${srcdir}/opt/acoinfo/${pkgbase}/pkg/base" "${pkgdir}/${_install_dir}"
}

package_realevo-linux-tools-lcsproxy() {
    backup=("etc/lcsproxy/lcsproxy.conf")

    ar x ${_lcsproxy_name}/lcsproxy-1.1.1-linux-amd64.deb
    tar --no-same-owner --no-same-permissions -xavf data.tar.gz -C ${pkgdir}
    mv ${pkgdir}/bin ${pkgdir}/usr
}

package_realevo-linux-tools-cmake-plugins() {
    for plugin in SylixOS.cmake SylixOS-GNU.cmake SylixOS-GNU-C.cmake SylixOS-GNU-CXX.cmake SylixOS-GNU-Fortran.cmake;do
        install -Dm644 "${srcdir}/opt/acoinfo/${pkgbase}/config/cmake/platform/${plugin}" "${pkgdir}/usr/share/cmake/Modules/Platform/${plugin}"
    done
}
